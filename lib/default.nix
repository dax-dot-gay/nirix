/**
  This is ripped directly from https://codeberg.org/BANanaD3V/niri-nix/src/branch/main/lib/default.nix and would be notably less possible without it :D
*/
{
    self,
    lib,
    nixpkgs,
}:
rec {
    # Taken from home-manager
    toKDL =
        { }:
        let
            inherit (lib)
                concatStringsSep
                mapAttrsToList
                any
                ;
            inherit (builtins)
                typeOf
                replaceStrings
                elem
                hasAttr
                ;

            # ListOf String -> String
            indentStrings =
                let
                    # Although the input of this function is a list of strings,
                    # the strings themselves *will* contain newlines, so you need
                    # to normalize the list by joining and resplitting them.
                    unlines = lib.splitString "\n";
                    lines = lib.concatStringsSep "\n";
                    indentAll = lines: concatStringsSep "\n" (map (x: "	" + x) lines);
                in
                stringsWithNewlines: indentAll (unlines (lines stringsWithNewlines));

            # String -> String
            sanitizeString = replaceStrings [ "\n" ''"'' ] [ "\\n" ''\"'' ];

            # OneOf [Int Float String Bool Null] -> String
            literalValueToString =
                element:
                let
                    isMainType =
                        element:
                        elem (typeOf element) [
                            "int"
                            "float"
                            "string"
                            "bool"
                            "null"
                        ];
                in
                lib.throwIfNot
                    (
                        isMainType element || (typeOf element == "set" && hasAttr "_raw" element && isMainType element._raw)
                    )
                    "Cannot convert value of type ${typeOf element} to KDL literal."
                    (
                        if typeOf element == "null" then
                            "null"
                        else if element == false then
                            "false"
                        else if element == true then
                            "true"
                        else if typeOf element == "string" then
                            ''"${sanitizeString element}"''
                        else if typeOf element == "set" then
                            toString element._raw
                        else
                            toString element
                    );

            # Attrset Conversion
            # String -> AttrsOf Anything -> String
            convertAttrsToKDL =
                name: attrs:
                let
                    optArgs = map literalValueToString (attrs._args or [ ]);
                    optProps = lib.mapAttrsToList (name: value: "${name}=${literalValueToString value}") (
                        attrs._props or { }
                    );

                    orderedChildren = lib.pipe (attrs._children or [ ]) [
                        (map (child: mapAttrsToList convertAttributeToKDL child))
                        lib.flatten
                    ];
                    unorderedChildren = lib.pipe attrs [
                        (lib.filterAttrs (
                            name: _:
                            !(elem name [
                                "_args"
                                "_props"
                                "_children"
                            ])
                        ))
                        (mapAttrsToList convertAttributeToKDL)
                    ];
                    children = orderedChildren ++ unorderedChildren;
                    optChildren = lib.optional (children != [ ]) ''
                        {
                        ${indentStrings children}
                        }'';
                in
                lib.concatStringsSep " " ([ name ] ++ optArgs ++ optProps ++ optChildren);

            # List Conversion
            # String -> ListOf (OneOf [Int Float String Bool Null])  -> String
            convertListOfFlatAttrsToKDL =
                name: list:
                let
                    flatElements = map literalValueToString list;
                in
                "${name} ${concatStringsSep " " flatElements}";

            # String -> ListOf Anything -> String
            convertListOfNonFlatAttrsToKDL = name: list: ''
                ${lib.concatStringsSep "\n" (map (x: convertAttributeToKDL name x) list)}
            '';

            # String -> ListOf Anything  -> String
            convertListToKDL =
                name: list:
                let
                    elementsAreFlat =
                        !any (
                            el:
                            elem (typeOf el) [
                                "list"
                                "set"
                            ]
                        ) list;
                in
                if elementsAreFlat then
                    convertListOfFlatAttrsToKDL name list
                else
                    convertListOfNonFlatAttrsToKDL name list;

            # Combined Conversion
            # String -> Anything  -> String
            convertAttributeToKDL =
                name: value:
                let
                    vType = typeOf value;
                in
                if
                    elem vType [
                        "int"
                        "float"
                        "bool"
                        "null"
                        "string"
                    ]
                then
                    "${name} ${literalValueToString value}"
                else if vType == "set" then
                    convertAttrsToKDL name value
                else if vType == "list" then
                    convertListToKDL name value
                else
                    throw ''
                        Cannot convert type `(${typeOf value})` to KDL:
                          ${name} = ${toString value}
                    '';
        in
        attrs: ''
            ${concatStringsSep "\n" (mapAttrsToList convertAttributeToKDL attrs)}
        '';

    mkNiriKDL = toKDL { };
    mkNullOr = type: lib.mkOption {
        type = lib.types.nullOr type;
        default = null;
    };
    mkBool = default: lib.mkOption {
        type = lib.types.bool;
        default = default;
    };
    mkEnum = choices: lib.mkOption {
        type = lib.types.enum choices;
        default = choices[0];
    };
    mkSub = options: lib.types.submodule ({...}: {options = options;});
    mkIfNotNull = value: lib.mkIf (!(isNull value)) value;

    validatedConfigFor =
        niri-package: config:
        builtins.readFile (
            nixpkgs.legacyPackages.${niri-package.system}.runCommand "config.kdl"
                {
                    inherit config;
                    passAsFile = [ "config" ];
                    buildInputs = [ niri-package ];
                }
                ''
                    grep -v '^include\s' $configPath > config-without-includes.kdl
                    niri validate -c config-without-includes.kdl
                    cp $configPath $out
                ''
        );

    _internal = rec {
        date = {
            year = builtins.substring 0 4;
            month = builtins.substring 4 2;
            day = builtins.substring 6 2;
            hour = builtins.substring 8 2;
            minute = builtins.substring 10 2;
            second = builtins.substring 12 2;
        };
        fmt-date = raw: "${date.year raw}-${date.month raw}-${date.day raw}";
    };
}
